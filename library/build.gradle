apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

}
buildscript {
    ext.kotlin_version = '1.0.0'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
repositories {
    mavenCentral()
}

apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/MeilCli/HKJson'
def gitUrl = 'https://github.com/MeilCli/HKJson.git'
def userName = 'meilcli'
def libraryId = 'HKJson'

def bintray_group = 'net.meilcli'
def bintray_artifact = 'hkjson'
def bintray_ver = '1.1.2'
def bintray_ver_desc = 'first bintray upload'

group = bintray_group
version = bintray_ver

bintray {
    user = userName
    key = project.property('bintray_api_key')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = libraryId
        userOrg = userName
        licenses = ['MIT']
        vcsUrl = gitUrl
        websiteUrl = siteUrl
        issueTrackerUrl = siteUrl + '/issues'
        labels = ['android', 'kotlin']
        version {
            name = bintray_ver
            desc = bintray_ver_desc
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            name 'This library is helper of JSONObject and JSONArray'
            url siteUrl
            groupId bintray_group
            artifactId bintray_artifact
            version bintray_ver
            developers {
                developer {
                    name userName
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
            licenses {
                license {
                    name 'The MIT License'
                    url 'https://github.com/MeilCli/HKJson/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}
